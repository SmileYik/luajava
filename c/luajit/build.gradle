plugins {
    id 'c'
}

group = 'org.keplerproject'
version = '1.0-SNAPSHOT'

def IS_WINDOWS = System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("win")
def IS_MACOS = System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("mac")

repositories {
    mavenCentral()
}

model {
    platforms {
        x86 {
            architecture "x86"
        }
        x64 {
            architecture "x86_64"
        }
        arm64 {
            architecture "aarch64"
        }
    }
}

model {
    components {
        luajit(NativeLibrarySpec) {
            if (IS_MACOS) {
                targetPlatform 'arm64'
            } else {
                targetPlatform 'x64'
            }
            
            sources {
                c {
                    exportedHeaders {
                        srcDir 'luajit/src'
                    }
                }
            }
        }

        luajava4Luajit(NativeLibrarySpec) {
            if (IS_MACOS) {
                targetPlatform 'arm64'
            } else {
                targetPlatform 'x64'
            }

            sources {
                c {
                    lib library: 'luajit', linkage: 'static'
                    exportedHeaders {
                        srcDir project(':c').layout.projectDirectory.dir('src/main/c')
                        srcDir "src/main/c"
                        srcDir "src/main/compatible"
                        srcDir layout.buildDirectory.dir('headers')
                        srcDir "${System.properties['java.home']}/include"
                        srcDir "${System.properties['java.home']}/include/linux"
                        srcDir "${System.properties['java.home']}/include/win32"
                        srcDir "${System.properties['java.home']}/include/darwin"
                    }
                    source {
                        srcDir project(':c').layout.projectDirectory.dir('src/main/c')
                        srcDir "src/main/c"
                        include "**/*.c"
                    }
                }
            }

            binaries.all {
                checkedBy $.tasks.buildLuaJIT
                checkedBy $.tasks.generateHeader
                def luajit_src = file('luajit/src')
                if (toolChain in VisualCpp) {
                    cppCompiler.define 'WIN64'
                    cppCompiler.define "LUAJAVA_VERSION", "\"$LUAJAVA_VERSION\""
                    linker.args "lua51.lib", "/LIBPATH:${luajit_src}"
                } else {
                    cCompiler.define "LUAJAVA_VERSION", "\"$LUAJAVA_VERSION\""
                    linker.args "-lluajit", "-L${luajit_src}"
                }
            }
        }
    }
}

tasks.register('generateHeader') {
    group("luajava")
    exec {
        workingDir project(':java').layout.projectDirectory.dir("src/main/java")
        commandLine file("${System.properties['java.home']}/bin/javac"),
                    "-cp", "./",
                    "-d", "${layout.buildDirectory.dir("classes").get()}",
                    "-h", "${layout.buildDirectory.dir("generated-headers").get()}",
                    "org/eu/smileyik/luajava/LuaState.java"
    }
    copy {
        from layout.buildDirectory.dir("generated-headers")
        into layout.buildDirectory.dir("headers")
        rename 'org_eu_smileyik_luajava_LuaState.h', 'luajava.h'
    }
}

tasks.register('cloneLuajit') {
    group("luajit")
    if (!file('luajit').exists()) {
        exec {
            workingDir '.'
            commandLine 'git', 'clone', 'https://luajit.org/git/luajit.git', 'luajit'
        }
    }
}

tasks.register('buildLuaJIT') {
    dependsOn cloneLuajit
    group("luajit")
    if (IS_WINDOWS) {
        exec {
            workingDir "$layout.projectDirectory"
            commandLine 'powershell', '-Command', '.\\buildLuajit.ps1'
        }
    } else {
        // TODO MACOS
        exec {
            workingDir "luajit"
            commandLine 'make'
        }
    }
}

build {
    doLast {
        copy {
            // linux
            from layout.buildDirectory.file("libs/luajava4Luajit/shared/")
            from layout.projectDirectory.file("luajit/src/libluajit.so")

            // macos
            from layout.projectDirectory.file("luajit/src/libluajit.dylib")

            // windows
            from layout.buildDirectory.file("libs/luajava/shared/luajava.dll")
            from layout.projectDirectory.file("luajit/src/lua51.dll")

            into rootProject.layout.buildDirectory.dir("outputs/shared/luajit")
        }
    }
}